plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
}

android {
    namespace "org.wordpress.aztec"

    compileSdkVersion 33

    defaultConfig {

        minSdkVersion 23
        targetSdkVersion 33
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                if (project.hasProperty('testsMaxHeapSize')) {
                    // maxHeapSize for tests is not limited unless we give an explicit value
                    // See https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
                    maxHeapSize project.properties.get('testsMaxHeapSize')
                }
            }
        }
    }

    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation "org.ccil.cowan.tagsoup:tagsoup:$tagSoupVersion"
    implementation "org.jsoup:jsoup:$jSoupVersion"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "org.wordpress:utils:$wordpressUtilsVersion"

    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation 'androidx.test:core:1.4.0'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    implementation 'org.apache.commons:commons-lang3:3.8.1'

    lintChecks "org.wordpress:lint:$wordpressLintVersion"
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId "io.dar.common"
                artifactId "aztec"
                version '1.8.1'
            }
        }
        repositories {
            maven {
                url = "https://nexus.dar.tech/repository/android/"
                credentials {
                    username (System.getenv("NEXUS_USER") != null ? System.getenv("NEXUS_USER") : NEXUS_USER)
                    password (System.getenv("NEXUS_PASS") != null ? System.getenv("NEXUS_PASS") : NEXUS_PASS)
                }
            }
        }
    }
}
